-- @atlcompiler emftvm
-- @nsURI AR=http://osate.org/Altarica
-- @nsURI AADL=http://aadl.info/AADL/2.0/instance
-- @nsURI AADL2=http://aadl.info/AADL/2.0
-- @nsURI EMV2=http://www.aadl.info/EMV2


module AADL2AltaRica;
create OUT: AR from IN: AADL;

-- Traverse component instances in post order
-- Nodes must be declared before use
rule SystemInstance {
	from
		si: AADL!SystemInstance
	to
		s: AR!System (
			ownedDeclarations <- thisModule.postOrder(si)->flatten()
					-> collect(ci | thisModule.makeNode(ci.componentClassifier(), ci))
					-> iterate(n; acc: Sequence(AR!Node) = Sequence{} | if n.oclIsUndefined() then acc else acc.append(n) endif)
		)
}

helper def: postOrder(ci: AADL!ComponentInstance): Sequence(AADL!ComponentInstance) =
	let
		subs: Sequence(AADL!ComponentInstance) = ci.componentInstance
	in
		subs -> iterate(sub; acc:Sequence(AADL!ComponentInstance) = Sequence{} |
				acc -> append(thisModule.postOrder(sub))
		).append(ci);

-- Create a node for the referenced classifier
-- This must be a rule because we need to modify the created node
lazy rule makeNode {
	from
		c: AADL2!ComponentClassifier,
	  ci: AADL!ComponentInstance (
	  	not thisModule.processedClassifiers.contains(c)
	  )
  using {
		n: AR!Node = thisModule.Node(c);
		specifications: Sequence(AR!AbstractSpecification) = Sequence{};
		eps: Sequence(EMV2!ErrorPropagation) = c.errorPropagations();
  }
	do {
		-- fill in flow variables
		if (not eps.isEmpty()) {
			specifications <- specifications -> including(thisModule.flows(c)) -> flatten();
		}
		-- fill in sub nodes
		if (not ci.componentInstance.isEmpty()) {
			specifications <- specifications -> including(thisModule.subNodes(ci)) -> flatten();
		}
		n.ownedSpecifications <- specifications;
		-- process each referenced classifier only once
		thisModule.processedClassifiers <:= thisModule.processedClassifiers.including(c);
		n;
	}
}

helper def: processedClassifiers: Set(AADL2!ComponentClassifier) = Set{};

-- Need this a a separate rule to resolve classifier
-- Alternative is to create a (classifier |-> node) map
unique lazy rule Node {
	from	
		c: AADL2!ComponentClassifier
	to
		n: AR!Node (
			-- AltaRica editor doesn't understand quoted IDs
			-- name <- c.qualifiedName().quoted()
			name <- c.qualifiedName().normalizeId()
		)
}

-- The sub-nodes section
lazy rule subNodes {
	from
		ci: AADL!ComponentInstance
	to
		nis: AR!NodeInstanceSpecification (
			ownedNodeInstances <- ci.componentInstance -> collect(s | thisModule.NodeInstance(s))
		)
}

lazy rule NodeInstance {
	from
		ci: AADL!ComponentInstance
	to
		ni: AR!NodeInstance (
			name <- ci.name,
			nodeType <- ci.componentClassifier().resolve('Node')
		)
}

-- The flows section
lazy rule flows {
	from
		c: AADL!ComponentClassifier
	to
		nis: AR!FlowSpecification (
			ownedFlows <- 
					c.debug().incomingErrorPropagations() -> collect(ep | c.makeFlows(ep, thisModule.flowDirectionIn))
					-> including(c.outgoingErrorPropagations() -> collect(ep | c.makeFlows(ep, thisModule.flowDirectionOut)))
					-> flatten()
		)
}

helper context AADL2!ComponentClassifier def: makeFlows(ep: EMV2!ErrorPropagation, dir: AR!FlowKind): Sequence(AR!Flow) =
	let
		tokens: Sequence(EMV2!TypeToken) = ep.getTypeSet().leafTypeTokens(self.usedTypes())
	in
		tokens -> collect(t | thisModule.Flow(ep, t.type -> first().name, dir));
	
lazy rule Flow {
	from
		ep: EMV2!ErrorPropagation,
--		leafToken: EMV2!TypeToken,
		tokenName: String,
		dir: AR!FlowKind
	to
		f: AR!Flow (
			-- name <- ep.debug().altaRicaName(leafToken.debug().type -> first().name.debug()),
			name <- ep.debug().altaRicaName(tokenName.debug()),
			domain <- thisModule.PrimitiveType(#bool),
			kind <- dir
		)
}

-- a bool or integer
lazy rule PrimitiveType {
	from
		kind: AR!PrimitiveTypeKind
	to
	  p: AR!PrimitiveType (
	  	name <- kind
		)
}

-- Replace invalid characters in identifier with '_'
helper context String def: normalizeId(): String =
	self.replaceAll('[:.]', '_');

helper context String def: quoted(): String =
	'"' + self + '"';

-- Get the component classifier for a compoent instance
helper context AADL!ComponentInstance def: componentClassifier(): AADL2!ComponentClassifier =
	"#native"!"org::osate::aadl2::instance::util::InstanceUtil".refInvokeStaticOperation(
		'getComponentClassifier', Sequence{self, 0, OclUndefined}
	);

-- Helpers for EMV2 processing
helper context AADL2!Classifier def: outgoingErrorPropagations(): Sequence(EMV2!ErrorPropagation) =
	"#native"!"org::osate::xtext::aadl2::errormodel::util::EMV2Util".refInvokeStaticOperation(
		'getAllOutgoingErrorPropagations', Sequence{self}
	);

helper context AADL2!Classifier def: incomingErrorPropagations(): Sequence(EMV2!ErrorPropagation) =
	"#native"!"org::osate::xtext::aadl2::errormodel::util::EMV2Util".refInvokeStaticOperation(
		'getAllIncomingErrorPropagations', Sequence{self}
	);

helper context AADL2!Classifier def: errorPropagations(): Sequence(EMV2!ErrorPropagation) =
	self.incomingErrorPropagations() -> including(self.outgoingErrorPropagations()) -> flatten();

helper context AADL2!Classifier def: usedTypes(): Sequence(EMV2!ErrorModelLibrary) =
	"#native"!"org::osate::xtext::aadl2::errormodel::util::EMV2Util".refInvokeStaticOperation(
		'getErrorModelSubclauseWithUseTypes', Sequence{self}
	);

helper context EMV2!ErrorPropagation def: altaRicaName(typeName: String): String =
	let
		ref: EMV2!FeatureorPPReference = self.getFeatureorPPRef()
	in
		ref.altaRicaName() + '_' + typeName;

helper context EMV2!FeatureorPPReference def: altaRicaName(): String =
	let
		name: String = self.featureorPP.name
	in
		if self.next.oclIsUndefined() then name else name + '_' + self.next.altaRicaName() endif;

helper context EMV2!TypeSet def: leafTypeTokens(useTypes: Sequence(EMV2!ErrorModelLibrary)): Sequence(EMV2!TypeToken) =
	"#native"!"org::osate::xtext::aadl2::errormodel::util::EM2TypeSetUtil".refInvokeStaticOperation(
		'generateAllLeafTypeTokens', Sequence{self, useTypes}
	);

helper def: flowDirectionOut: AR!FlowKind = #out;
helper def: flowDirectionIn: AR!FlowKind = #"in";


